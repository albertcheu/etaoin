S -> BACKGROUND 'is' C | BASICASSN | ADJASSN | ASSNCOL | NPSING 'has' NUM SIDES | THERE NQ
BASICASSN -> NA PP | NA NP | NA C
ADJASSN -> NARE ADJACENT UNITE | NIS ADJACENT NP
ASSNCOLS -> NPSING SAMECOLS | NPSING SAMECOLS AS NP
ASSNCOLP -> NPPLUR SAMECOLP | NPPLUR SAMECOLP AS NP
ASSNCOL -> ASSNCOLS | ASSNCOLP

NIS -> NPSING 'is'
NARE -> NPPLUR 'are'
NA -> NIS | NARE

ISN -> 'is' NPSING
AREN -> 'are' NPPLUR
NQ -> ISN | AREN

NSING -> GENERICS | SPECIFICS
GENERICS -> 'polygon' | 'shape' | 'figure' | 'object'
SPECIFICS -> 'triangle' | 'quadrilateral' | 'pentagon' | 'hexagon' | 'octagon'

NPLUR -> GENERICP | SPECIFICP
GENERICP -> 'polygons' | 'shapes' | 'figures' | 'objects'
SPECIFICP -> 'triangles' | 'quadrilaterals' | 'pentagons' | 'hexagons' | 'octagons'

ENUMSING -> THE | 'a' | 'an' | ONE | EACH | 'except' FOR WHOLENUM EVERY
ENUMPLUR -> THE | NUM | ALL | 'except' FOR WHOLENUM ALL | ALL 'other'

NP -> NPSING | NPPLUR

NPSING -> NPSINGSEC | NPSINGSEC PP
NPSINGSEC -> NPSINGTERT | ENUMSING NPSINGTERT
NPSINGTERT -> NSING | C NSING

NPPLUR -> NPPLURSEC | NPPLURSEC PP
NPPLURSEC -> NPPLUR1 | NPPLUR2
NPPLUR1 -> NPPLUR1SEC | ENUMPLUR NPPLUR1SEC
NPPLUR1SEC -> C NPLUR | NPLUR
NPPLUR2 -> NPSING 'and' NPSING | NPSING 'and' NPPLUR1 | NPPLUR1 'and' NPPLUR1 | NPPLUR1 'and' NPSING

PP -> PPS | PPS 'and' PPS
PPS -> P1 | P2 NPALT
NPSEC -> NPSINGSEC | NPPLURSEC
NPALT -> NPSEC | NPSEC P1

P1 -> AT THE GLOBALLOC
GLOBALLOC -> 'center' | HORIZ | VERT | CORNERLOC
HORIZ -> 'left' | 'right'
CORNERLOC -> VERT HORIZ
VERT -> 'top' | 'bottom'

P2 -> CARDINAL | ADJACENT
ADJACENT -> 'near'
CARDINAL -> AT THE HORIZ OF | 'above' | 'below'

THERE -> 'there'

ON -> 'on'
OF -> 'of'
FOR -> 'for'
THE -> 'the'
TO -> 'to'

COLOR -> 'color'
SIDES -> 'sides'

AT -> 'at'
NO -> 'no'
NOT -> 'not'
ALL -> 'all'
EVERY -> 'every'
THING -> 'something'
BACKGROUND -> THE 'background'


SAMECOLS -> 'shares' THE 'same' COLOR
SAMECOLP -> 'share' THE 'same' COLOR
AS -> 'as'
UNITE -> ONE 'another' | EACH 'other'

C -> 'red' | 'orange' | 'yellow' | 'green' | 'blue' | 'violet' | 'pink' | 'cyan' | 'black' | 'white' | 'gray' | 'brown' | 'tan'

EACH -> 'each'
ONE -> 'one'
NUM -> 'two' | 'three' | 'four' | 'five' | 'six' | 'seven' | 'eight' | 'nine'
WHOLENUM -> ONE | NUM