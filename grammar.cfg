S -> ASSERTION | QUESTION

NIS -> NPSING 'is'
NARE -> NPPLUR 'are'
NA -> NIS | NARE
ASSERTION -> NA PP | NA NP | NA C | NARE ADJACENT UNITE | NIS ADJACENT NP | ASSNCOL | NPSING 'has' NUM SIDES | THERE NQ | PP THERE NQ | BACKGROUND 'is' C
ASSNCOLS -> NPSING SAMECOLS | NPSING SAMECOLS AS NP | NPSING SAMECOLS AS UNITE
ASSNCOLP -> NPPLUR SAMECOLP | NPPLUR SAMECOLP AS NP | NPPLUR SAMECOLP AS UNITE
ASSNCOL -> ASSNCOLS | ASSNCOLP

QUESTION -> BOOLQ | COUNTQ | FETCHQ

ISN -> 'is' NPSING
AREN -> 'are' NPPLUR
NQ -> ISN | AREN
BOOLQ -> 'is' BACKGROUND C | BOOLPLUR | BOOLSING
BOOLPLUR -> 'do' NPPLUR SAMECOLP | 'do' NPPLUR SAMECOLP AS UNITE | 'do' NPPLUR SAMECOLP AS UNITE | PP 'are' THERE NPPLUR | 'are' THERE NPPLUR | AREN C | AREN NPPLUR | AREN PP | AREN ADJACENT UNITE
BOOLSING -> 'is' THERE THING PP | 'does' NPSING SAMECOLP AS NP | 'does' NPSING 'have' NUM SIDES | PP 'is' THERE NPSING | 'is' THERE NPSING | ISN C | ISN NPSING | ISN PP

COUNTQ -> HOW MANY NARE CQ1 | HOW MANY NPPLUR CQ2
CQ1 -> THERE | C | PP | ADJACENT UNITE
CQ2 -> SAMECOLP | SAMECOLP AS UNITE | SAMECOLP AS NP

FETCHQ ->  FETCHQSING | WHAT 'is' PP | WHERE AREN | WHAT COLOR AREN | WHAT 'is' THE COLOR OF BACKGROUND | WHAT COLOR 'is' BACKGROUND
FETCHQSING -> WHAT 'is' THE COLOR OF NPSING | WHAT 'is' THE NUMBER OF SIDES OF NPSING | WHAT COLOR ISN | WHERE ISN | HOW MANY SIDES 'does' NPSING 'have'

ENUMSING -> THE | 'a' | 'an' | ONE | EACH | EVERY | 'except' FOR WHOLENUM EVERY | NOT EVERY | NO
ENUMPLUR -> THE | THE NUM | NUM | ALL | 'except' FOR WHOLENUM ALL | NOT ALL | ALL 'other'

NSING -> GENERICS | SPECIFICS
GENERICS -> 'polygon' | 'shape' | 'figure' | 'object'
SPECIFICS -> 'triangle' | 'quadrilateral' | 'pentagon' | 'hexagon' | 'octagon'

NPLUR -> GENERICP | SPECIFICP
GENERICP -> 'polygons' | 'shapes' | 'figures' | 'objects'
SPECIFICP -> 'triangles' | 'quadrilaterals' | 'pentagons' | 'hexagons' | 'octagons'

NP -> NPSING | NPPLUR

NPSING -> NPSINGSEC | NPSINGSEC PP | THING | THING PP
NPSINGSEC -> NPSINGTERT | ENUMSING NPSINGTERT
NPSINGTERT -> NSING | C NSING

NPPLUR -> NPPLURSEC | NPPLURSEC PP
NPPLURSEC -> NPPLUR1 | NPPLUR2
NPPLUR1 -> NPPLUR1SEC | ENUMPLUR NPPLUR1SEC
NPPLUR1SEC -> C NPLUR | NPLUR
NPPLUR2 -> NPSINGSEC 'and' NPSINGSEC | NPSINGSEC 'and' NPPLUR1 | NPPLUR1 'and' NPPLUR1 | NPPLUR1 'and' NPSINGSEC

PP -> PPS | PPS 'and' PPS
PPS -> P1 OF THE SCREEN | P1 | P2 NPALT
NPSEC -> NPSINGSEC | NPPLURSEC
NPALT -> NPSEC | NPSEC P1 | NPSEC P1 OF THE SCREEN

P1 -> ATTO THE GLOBALLOC | 'in' THE GLOBALLOC | ON THE GLOBALLOC
GLOBALLOC -> 'center' | HORIZ | 'top' | 'bottom' | CORNERLOC
HORIZ -> 'left' | 'right'
CORNERLOC -> CORNERMOD HORIZ | CORNERMOD HORIZ 'corner'
CORNERMOD -> 'upper' | 'top' | 'lower' | 'bottom'

P2 -> CARDINAL | ADJACENT
ADJACENT -> 'close' TO | 'next' TO | 'near'
CARDINAL -> ATTO THE HORIZ OF | 'above' | ON 'top' OF | 'atop' | 'below' | 'beneath'

WHAT -> 'what'
WHERE -> 'where'
HOW -> 'how'
MANY -> 'many'
THERE -> 'there'

ON -> 'on'
OF -> 'of'
FOR -> 'for'
THE -> 'the'
TO -> 'to'

COLOR -> 'color'
SIDES -> 'sides'
SCREEN -> 'screen'

ATTO -> 'at' | TO
NO -> 'no'
NOT -> 'not'
ALL -> 'all'
EVERY -> 'every'
THING -> 'something' | 'anything'
BACKGROUND -> THE 'background'

SHARES -> 'is' | 'has' | 'shares'
SHAREP -> 'are' | 'have' | 'share'
SAMECOLS -> SHARES THE 'same' COLOR
SAMECOLP -> SHAREP THE 'same' COLOR
AS -> 'as'
UNITE -> ONE 'another' | EACH 'other'

C -> 'red' | 'orange' | 'yellow' | 'green' | 'blue' | 'violet' | 'pink' | 'cyan' | 'black' | 'white' | 'gray' | 'brown' | 'tan'

EACH -> 'each'
ONE -> 'one'
TWO -> 'two'
NUM -> 'two' | 'three' | 'four' | 'five' | 'six' | 'seven' | 'eight' | 'nine'
WHOLENUM -> ONE | NUM